apply plugin: 'com.android.library'
apply plugin: 'maven'

// This property value is for couchbase-lite-java-forestdb
ext.spec = "android"

version = System.getenv("MAVEN_UPLOAD_VERSION")

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
    }
}

repositories {
    mavenLocal()
    maven { url 'http://files.couchbase.com/maven2/' }
    mavenCentral()
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }
    
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        abortOnError false
    }

    // workaround for "duplicate files during packaging of APK" issue
    // see https://groups.google.com/d/msg/adt-dev/bl5Rc4Szpzg/wC8cylTWuIEJ
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

def buildAndroidWithArtifacts = System.getProperty("buildAndroidWithArtifacts")

dependencies {
    compile buildAndroidWithArtifacts == null ?
            project(':couchbase-lite-java-core') :
            'com.couchbase.lite:couchbase-lite-java-core:1.3.0'

    compile 'com.couchbase.lite:couchbase-lite-android-sqlite-custom:1.3.0'

    // Libraries required for tests only:
    androidTestCompile 'com.couchbase.lite:couchbase-lite-android-sqlcipher:1.3.0'

    androidTestCompile 'com.couchbase.lite:couchbase-lite-android-forestdb:1.3.0'

    androidTestCompile 'commons-io:commons-io:2.0.1'
    androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.3.1'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    List<File> pathList = new ArrayList<File>();
    pathList.add(file('./libraries/couchbase-lite-java-core/extra/doclet/doclet.jar'))
    options.docletpath = pathList
    options.doclet = "ExcludeDoclet"
    options.showFromPublic()
    exclude "org/apache/http/**", "com/couchbase/touchdb/**"
}

task createMavenDirectory(type: Exec) {
    //these envs are required only to upload maven artifacts
    print project.getGradle()
    if (project.getGradle().toString().contains("createMavenDirectory")) {
        if (System.getenv("MAVEN_UPLOAD_REPO_URL") == null) {
            throw new GradleException("Please set MAVEN_UPLOAD_REPO_URL !!!")
        }
        if (System.getenv("MAVEN_UPLOAD_USERNAME") == null) {
            throw new GradleException("Please set MAVEN_UPLOAD_USERNAME !!!")
        }
        if (System.getenv("MAVEN_UPLOAD_PASSWORD") == null) {
            throw new GradleException("Please set MAVEN_UPLOAD_PASSWORD !!!")
        }
        if (System.getenv("MAVEN_UPLOAD_VERSION") == null) {
            throw new GradleException("Please set MAVEN_UPLOAD_VERSION !!!")
        }
    }

    ext {
        uploadUser = System.getenv("MAVEN_UPLOAD_USERNAME") + ":" + System.getenv("MAVEN_UPLOAD_PASSWORD")
        mkcolPath = System.getenv("MAVEN_UPLOAD_REPO_URL") + "com/couchbase/lite/couchbase-lite-android/" + System.getenv("MAVEN_UPLOAD_VERSION") + "/"
    }
    commandLine "curl", "--user", uploadUser, "-X", "MKCOL", mkcolPath
}

// this hack is only needed for apache mod_dav based Maven repo's like file.couchbase.com.  otherwise, skip it
createMavenDirectory.onlyIf { System.getenv("MAVEN_UPLOAD_REPO_URL").contains("files") }

// first create the directory, then do the upload
task uploadArchivesWrapper(dependsOn: createMavenDirectory) << {
    uploadArchives.execute()
}

def mavenPath() {
    System.getenv("MAVEN_BUILD_LOCAL") == "true" ?
            'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath :
            System.getenv("MAVEN_UPLOAD_REPO_URL")
}

// this will upload, but will not first create a directory (which is needed on some servers)
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: mavenPath()) {
                authentication(userName: System.getenv("MAVEN_UPLOAD_USERNAME"), password: System.getenv("MAVEN_UPLOAD_PASSWORD"))
            }
            pom.version = System.getenv("MAVEN_UPLOAD_VERSION") != null ? System.getenv("MAVEN_UPLOAD_VERSION") : ''
            pom.groupId = 'com.couchbase.lite'
            pom.artifactId = 'couchbase-lite-android'
            pom.project {
                licenses {
                    license {
                        name 'Couchbase Community Edition License Agreement'
                        url 'http://www.couchbase.com/agreement/community'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}
